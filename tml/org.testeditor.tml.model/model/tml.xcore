/*******************************************************************************
 * Copyright (c) 2012 - 2016 Signal Iduna Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * Signal Iduna Corporation - initial API and implementation
 * akquinet AG
 * itemis AG
 *******************************************************************************/
@Ecore(nsURI="http://www.testeditor.org/tml")
@GenModel(modelDirectory="org.testeditor.tml.model/src-gen", 
	complianceLevel="7.0")
package org.testeditor.tml

import org.eclipse.xtext.xtype.XImportSection
import org.testeditor.aml.Template
import org.testeditor.aml.Component
import org.testeditor.tsl.StepContent
import org.testeditor.aml.TemplateVariable

class TmlModel {
	String ^package
	contains XImportSection importSection
	String name
	contains Macro[0..*] macros
}

class Macro {
	String name
	contains Template template
	contains TestStepContext[0..*] contexts
}

abstract class TestStepContext {
}

class ComponentTestStepContext extends TestStepContext{
	refers Component component
	contains TestStep[0..*] steps
}

class MacroTestStepContext extends TestStepContext{
	refers TmlModel macroModel
	contains TestStep step
}

class TestStep {
	contains StepContent[0..*] contents
}

class AssertionTestStep extends /*Component*/TestStep {
	contains AssertionExpression expression
}

abstract class AssertionExpression {
}

class BinaryAssertionExpression extends AssertionExpression{
	contains AssertionExpression left
	contains AssertionExpression right
}

class AEComparison extends BinaryAssertionExpression {
	contains Comparator comparator
}

class AENullCheck extends AssertionExpression {
	contains AEVariableReference varReference
	boolean negated = "false"
}

class AEVariableReference extends AssertionExpression {
	String name
	String key // only != null if: map.key
}

class AEStringConstant extends AssertionExpression {
	String string
}

class TestStepWithAssignment extends TestStep {
	String variableName
}

class StepContentVariableReference extends StepContent{
	refers TemplateVariable variable
}

class StepContentElement extends StepContent {
}

class Comparator {
	boolean negated = "false"
}

class ComparatorEquals extends Comparator {
}

class ComparatorMatches extends Comparator {
}

class ComparatorLessThen extends Comparator {
}

class ComparatorGreaterThen extends Comparator {
}