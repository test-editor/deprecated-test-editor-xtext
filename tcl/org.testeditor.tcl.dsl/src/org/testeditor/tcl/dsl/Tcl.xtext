/*******************************************************************************
 * Copyright (c) 2012 - 2018 Signal Iduna Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * Signal Iduna Corporation - initial API and implementation
 * akquinet AG
 * itemis AG
 *******************************************************************************/
grammar org.testeditor.tcl.dsl.Tcl with org.eclipse.xtext.xbase.Xbase hidden(SPACES)

import "http://www.testeditor.org/tcl"
import "http://www.testeditor.org/tsl" as tsl
import "http://www.testeditor.org/aml" as aml
import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.eclipse.org/xtext/common/JavaVMTypes"



TclModel:
	{TclModel}
	(XL? 'package' XL? package=QualifiedName)?
	(XL? importSection=TclImportSection)?
	// TODO move require to TestCase - this should not be part of a MacroCollection
	(XL? 'require' XL? environmentVariables+=EnvironmentVariable (',' XL? environmentVariables+=EnvironmentVariable)*)?
	(XL? (macroCollection=MacroCollection | 
		test=TestCase | config=TestConfiguration ))
	XL?;

/*
 * This terminal is used in addition to the whitespace token definitions of the base grammar xbase,
 * which are WS, ML_COMMENT and SL_COMMENT. Since WS includes spaces and newlines and this grammar
 * makes use of newlines to identify certain grammar elements, XL is introduced, which holds at least
 * one newline and can be surrounded by white spaces (space, tabs) and be prefixed by any number of
 * single line comments and or multiline comments.
 * 
 * The grammar itself treats SPACES (which are tabs and/or spaces) as hidden tokens whereas XL (and thus
 * any newline) is treated explicitly within the grammar and must therefore be explicitly added to
 * rules that allow new lines in certain places 
 */
terminal XL:
	(( ' ' | '\t')* ('/*' -> '*/' ( ' ' | '\t')*)* ('//' !('\r'|'\n')*)? ('\r'? '\n'))+  ( ' ' | '\t' )*;

/*
 * ImportSection and XImportDeclaration were copied from xbase in order to prevent problems in the whitespace
 * dependend lookahead of this grammar, related with the whitespace independent lookahead of these rules
 * within the xbase grammar. Ambiguities are not reported, but parsing some examples result in errors, because
 * the lookahead uses whitespaces a bit differently than the overall grammar might suggest. 
 */
TclImportSection returns xtype::XImportSection:
	importDeclarations+=TclXImportDeclaration (XL? importDeclarations+=TclXImportDeclaration)*;
			
TclXImportDeclaration returns xtype::XImportDeclaration:
	'import' (
		(static?='static' extension?='extension'? importedType=[JvmDeclaredType|QualifiedNameInStaticImport] (wildcard?='*' | memberName=ValidID))
		| importedType=[JvmDeclaredType|QualifiedName]
		| importedNamespace=QualifiedNameWithWildcard) ';'?
;
	
TestCase:
	{TestCase}
	'#' name=ID
	(XL? 'implements' XL? specification=[tsl::TestSpecification|QualifiedName])?
	(XL? 'config' XL? config=[TestConfiguration])?
	( // allow user to define setup and cleanup either at the beginning or end
  	  // unordered groups don't compose with XL, so validation has to check
  	  // that (max) only one setup/cleanup is specfied (either before the steps, or after the steps)
		(XL setup+=TestSetup)?
	 	(XL cleanup+=TestCleanup)?
	 	(XL steps+=SpecificationStepImplementation
	 		(XL steps+=SpecificationStepImplementation)*
	 		(XL cleanup+=TestCleanup)?
			(XL setup+=TestSetup)?	
		)?
	);

TestConfiguration:
	'config' name=ID
	((XL setup+=TestSetup)? & (XL cleanup+=TestCleanup)?);

TestSetup:
	{TestSetup}
	'Setup' ':'
	(XL contexts+=TestStepContext)*
;

TestCleanup:
	{TestCleanup}
	'Cleanup' ':'
	(XL contexts+=TestStepContext)*
;

SpecificationStepImplementation:
	{SpecificationStepImplementation}
	'*' contents+=tslInnerStepContent+
	  (XL contents+=tslStepContent contents+=tslInnerStepContent* )* 
	(XL contexts+=TestStepContext)*;

/*
 * Inner step content text allows more tokens than step content (which might have a new line right before
 * its token, which in turn might belong to a different semantical unit (e.g. 'Component')
 */
tslInnerStepContent returns tsl::StepContent:
	tslInnerStepContentText | StepContentVariable;

tslInnerStepContentText returns tsl::StepContentText:
	value=(Word | '*' | InnerStepContentTextOperator);

tslStepContent returns tsl::StepContent:
	tslStepContentText | StepContentVariable;

tslStepContentText returns tsl::StepContentText:
	value=(Word | StepContentTextOperator);

Word returns ecore::EString hidden():
	ID | INT | '@' | ',' | '|' | '.' | '?' | '!' | '#' | '##' | '${' | '%' | '&' | '(' | ')' | '-' | '=' |
	'+' | '`' | '~' | '\\' | ']' | '[' | '}' | '{' | ';' | ':' | '<' | '>' | '<>' | '/' | '==' | '!=' | '>=' | '<=' |
	'assert' | 'matches' | 'true' | "\\'" | '\\"' | '$' | '_' |
	 ANY_OTHER;

EnvironmentVariable:
	(nonConfidential?='public')?
	name=ID;

MacroCollection:
	'#' name=ID
	(XL? macros+=Macro)+;

Macro:
	'##' name=ID
	XL? 'template' XL? '=' XL? template=Template
	(XL contexts+=TestStepContext)*
;

Template returns aml::Template:
	contents+=TemplateText (XL? contents+=TemplateVariable (XL? contents+=TemplateText)?)*
;

TemplateText returns aml::TemplateText:
	value=STRING
;

TemplateVariable returns aml::TemplateVariable:
	name=TEMPLATE_VARIABLE_DEF
;

TestStepContext:
	(ComponentTestStepContext | MacroTestStepContext);

ComponentTestStepContext returns TestStepContext:
	{ComponentTestStepContext}
	('Mask' | 'Component') ':' component=[aml::Component|QualifiedName]
	(XL steps+=(TestStep | TestStepWithAssignment | AssertionTestStep | AssignmentThroughPath))*;

MacroTestStepContext returns TestStepContext:
	{MacroTestStepContext}
	'Macro' ':' macroCollection=[MacroCollection|QualifiedName]
	(XL steps+=TestStep)*; // currently macro calls can only be TestSteps!

/**
 * keep in mind that an aml validator exists that will check templates to be parsable given the following rule
 */
TestStep:
	{TestStep}
	'-' contents+=InnerStepContent+
		(XL contents+=StepContent contents+=InnerStepContent*)* 
	(XL? contents+=StepContentPunctuation)?;

AssertionTestStep:
	'-' 'assert' assertExpression=(NullOrBoolCheck | FullComparison) '.'?;

TestStepWithAssignment:
	'-' variable=AssignmentVariable XL? '='  
		XL? contents+=InnerStepContent+
			(XL contents+=StepContent contents+=InnerStepContent*)* 
	contents+=StepContentPunctuation?;

AssignmentThroughPath:
	'-' variableReference=VariableReferencePathAccess XL? '=' 
	    XL? expression=Expression '.'?;

/** expression order: Comparison -> (Addition -> Multiplication ->) Value
 *  which is reflecting the order of operator binding.
 *
 *  for binary expression: left is always referencing the rule with higher operator binding,
 *  right is referencing the Expression
 */
Expression:
	Comparison;

NullOrBoolCheck:
	(negated?='!')? variableReference=(VariableReference | VariableReferencePathAccess);

FullComparison returns Comparison:
	left=Value
	XL? comparator=Comparator
	XL? right=Expression;

Comparison:
	left=Value
	(XL? comparator=Comparator
	 XL? right=Expression)?;

VariableReference:
	variable=[aml::Variable];


ArrayPathElement:
	'[' number=Number ']';

KeyPathElement:
	'.' ( key=ID | key=STRING );

VariableReferencePathAccess:
	variable=[aml::Variable] ( XL? path+=KeyPathElement | XL? path+=ArrayPathElement )+;

AtomicValue returns Expression:
	VariableReference | VariableReferencePathAccess | JsonValue ;

Value returns Expression:
	AtomicValue | '(' Expression ')';

Comparator:
	ComparatorEquals |
	ComparatorMatches |
	ComparatorLessThan |
	ComparatorGreaterThan;

ComparatorEquals:
	{ComparatorEquals} ('=' | '==' | negated?='!=' | negated?='<>' | 'is' (negated?='not')?);

ComparatorMatches:
	{ComparatorMatches} ('matches' | negated?='does' 'not' 'match');

ComparatorLessThan:
	{ComparatorLessThan} ('<' | negated?='>=');

ComparatorGreaterThan:
	{ComparatorGreaterThan} ('>' | negated?='<=');

AssignmentVariable:
	name=ID;

InnerStepContent returns tsl::StepContent:
	InnerStepContentText | StepContentElement | StepContentVariable | '@' VariableReference | '@' VariableReferencePathAccess;


StepContent returns tsl::StepContent:
	StepContentText | StepContentElement | StepContentVariable | '@' VariableReference | '@' VariableReferencePathAccess;

InnerStepContentText returns tsl::StepContentText:
	value=ID | value=InnerStepContentTextOperator;


StepContentText returns tsl::StepContentText:
	value=ID | value=StepContentTextOperator;

/* 
 * hold all keywords that are allowed as step content text (to prevent parser to fail on keyword usage)
 * BUT excluding all keywords that may in combination with a newline be interpreted as an actual token (as Component) 
 */
StepContentTextOperator returns ecore::EString:
	// TODO generate this (all keywords minus assert - consider Xbase keywords as well)
	'var' | 'public' | 'template' | 'package' | 'require' | 'config' | 'implements' | 
	'does' | 'not' | 'match' | 'false' | 'null' |
	'as' |  'is' | 'for' | 'do' | 'if' | 'while';

// includes the keywords which may start a new section (if prefixed by a new line!)
InnerStepContentTextOperator returns ecore::EString:
	StepContentTextOperator | 'Setup' | 'Cleanup' |
	'Mask' | 'Component' | 'Macro' ;

StepContentVariable returns tsl::StepContentVariable:
	value=STRING;

StepContentElement hidden():
	('<' value=ID '>') |
	{StepContentElement} ('<>' | '<' (SPACES | XL ) '>');

StepContentPunctuation hidden():
	value=('.'|'?')
;

JsonObject:
	{JsonObject}
	'{' XL?
		(members+=JsonMember
		(',' XL? members+=JsonMember)* 
		XL?)?
	'}';

JsonMember:
	key=STRING ':' XL? value=JsonValue;

JsonValue:
	JsonString | JsonNumber | JsonObject | JsonArray | JsonBoolean | JsonNull;

JsonString:
	value=STRING;

JsonBoolean:
	value=Boolean;

JsonNull:
	{JsonNull}
	'null';

JsonArray:
	{JsonArray}
	'[' XL?
		(values+=JsonValue 
			(',' XL? values+=JsonValue)*
		XL?)?
	']';

JsonNumber:
	value=JsonDecimal;

Boolean returns ecore::EBoolean:
	'true' | 'false';

JsonDecimal returns ecore::EString hidden():
	'-'? Number;

/*
 * Using a separate terminal is required since we'd like to write ${element} and
 * this would otherwise be matched as the keyword 'element'.
 */
terminal TEMPLATE_VARIABLE_DEF:
	'${' ID? '}';

/*
 * override ID as provided by xbase to allow for (partial) unicode ids
 */
@Override
terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_' |  'À'..'ɿ' | 'Α'..'ѯ' | 'µ') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9' | 'À'..'ɿ' | 'Α'..'ѯ' | 'µ')*;

terminal SPACES:
	( ' ' | '\t' )+;
