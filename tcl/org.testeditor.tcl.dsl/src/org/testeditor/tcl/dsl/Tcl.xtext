/*******************************************************************************
 * Copyright (c) 2012 - 2015 Signal Iduna Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * Signal Iduna Corporation - initial API and implementation
 * akquinet AG
 * itemis AG
 *******************************************************************************/
grammar org.testeditor.tcl.dsl.Tcl with org.eclipse.xtext.xbase.Xbase



import "http://www.testeditor.org/tcl"
import "http://www.testeditor.org/tsl" as tsl
import "http://www.testeditor.org/aml" as aml
import "http://www.eclipse.org/xtext/common/JavaVMTypes" as types
import "http://www.eclipse.org/xtext/xbase/Xtype" as xtype
import "http://www.eclipse.org/emf/2002/Ecore" as ecore


TclModel:
	'package' package=QualifiedName
	(importSection=XImportSection)?
	test=TestCase
;

TestCase: 
	'#' name=ID
	('implements' specification=[tsl::TestSpecification|QualifiedName])?
	steps+=SpecificationStepImplementation*
;


SpecificationStepImplementation: 
	{SpecificationStepImplementation}
	'*' contents+=StepContent* '.'?
	contexts+=TestStepContext*
;

TestStepContext:
	('Mask' | 'Component') ':' component=[aml::Component]
	steps+=(TestStep | TestStepWithAssignment)*
;

TestStep:
	{TestStep}
	'-' contents+=StepContent* '.'?
;

TestStepWithAssignment: 
	'-' variableName=ID '=' contents+=StepContent* '.'?
;

StepContent returns tsl::StepContent: 
	StepContentText | StepContentElement | StepContentVariable
;

StepContentText returns tsl::StepContentText: 
	value=ID
;

StepContentVariable returns tsl::StepContentVariable:
	value=STRING 
;

StepContentElement hidden():
	('<' value=ID '>') |
	{StepContentElement} ('<>' | '<' WS+ '>')
;
