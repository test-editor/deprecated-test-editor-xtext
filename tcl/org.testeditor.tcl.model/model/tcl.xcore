@Ecore(nsURI="http://www.testeditor.org/tcl")
@GenModel(
	modelDirectory="org.testeditor.tcl.model/src-gen", 
	complianceLevel="7.0"
)

package org.testeditor.tcl

import org.eclipse.xtext.xtype.XImportSection
import org.testeditor.aml.model.Component

class TclModel {
	String ^package
	contains XImportSection importSection
	contains SpecificationStep[0..*] steps
	
	derived String name get {
		val lastSegment = eResource?.URI?.lastSegment
		if (lastSegment !== null) {
			val separator = lastSegment.lastIndexOf('.')
			if (separator >= 0) {
				return lastSegment.substring(0, separator).toFirstUpper
			} else {
				return lastSegment.toFirstUpper
			}
		}
		return null
	}
}

class SpecificationStep {
	contains StepContent[0..*] contents
	contains TestStepContext[0..*] contexts
}

class TestStepContext {
	refers Component component
	contains TestStep[0..*] steps
}

class TestStep {
	contains StepContent[0..*] contents
}

abstract class StepContent {
	String value
}

class StepContentText extends StepContent {
}

class StepContentVariable extends StepContent {
}