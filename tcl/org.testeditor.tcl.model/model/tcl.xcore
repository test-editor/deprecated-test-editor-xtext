/*******************************************************************************
 * Copyright (c) 2012 - 2015 Signal Iduna Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * Signal Iduna Corporation - initial API and implementation
 * akquinet AG
 * itemis AG
 *******************************************************************************/
@Ecore(nsURI="http://www.testeditor.org/tcl")
@GenModel(
	modelDirectory="org.testeditor.tcl.model/src-gen", 
	complianceLevel="7.0"
)
package org.testeditor.tcl

import org.eclipse.xtext.xtype.XImportSection
import org.testeditor.aml.Component
import org.testeditor.tsl.SpecificationStep
import org.testeditor.tsl.StepContent
import org.testeditor.tsl.TestSpecification

class TclModel {
	String ^package
	contains XImportSection importSection
	contains TestCase test opposite model
}

class TestCase {
	String name
	container derived TclModel model opposite test
	refers TestSpecification specification
	contains SpecificationStepImplementation[0..*] steps opposite test
}

class SpecificationStepImplementation extends SpecificationStep {
	container derived TestCase test opposite steps
	contains TestStepContext[0..*] contexts
}

class TestStepContext { }

class TestStepComponentContext extends TestStepContext{
	refers Component component
	contains ComponentTestStep[0..*] steps opposite context
}

class TestStepMacroContext extends TestStepContext{
	String macroId
	contains MacroTestStep step opposite context
}

class TestStep {
	contains StepContent[0..*] contents
}

class ComponentTestStep extends TestStep{
	container derived TestStepComponentContext context opposite steps
}

class MacroTestStep extends TestStep {
	container derived TestStepMacroContext context opposite step
}

class AssertionTestStep extends ComponentTestStep {
	contains AssertionExpression expression
}

class AssertionExpression {}

class BinaryAssertionExpression extends AssertionExpression{ 
	contains AssertionExpression left
	contains AssertionExpression right
}

class AEComparison extends BinaryAssertionExpression {
	contains Comparator comparator
}
class AENullCheck extends AssertionExpression {
	contains AEVariableReference varReference
	boolean negated = "false"
}

class AEVariableReference extends AssertionExpression {
	String name
	String key // only != null if: map.key
}

class AEStringConstant extends AssertionExpression {
	String string
}

class TestStepWithAssignment extends ComponentTestStep {
	String variableName
}

class StepContentElement extends StepContent {}
class Comparator {
	boolean negated = "false"
}
class ComparatorEquals extends Comparator {}
class ComparatorMatches extends Comparator {}
class ComparatorLessThen extends Comparator {}
class ComparatorGreaterThen extends Comparator {}