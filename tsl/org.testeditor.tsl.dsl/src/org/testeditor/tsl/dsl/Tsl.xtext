/*******************************************************************************
 * Copyright (c) 2012 - 2018 Signal Iduna Corporation and others.
 * All rights reserved. This program and the accompanying materials
 * are made available under the terms of the Eclipse Public License v1.0
 * which accompanies this distribution, and is available at
 * http://www.eclipse.org/legal/epl-v10.html
 *
 * Contributors:
 * Signal Iduna Corporation - initial API and implementation
 * akquinet AG
 * itemis AG
 *******************************************************************************/
grammar org.testeditor.tsl.dsl.Tsl hidden(SPACES)

import "http://www.eclipse.org/emf/2002/Ecore" as ecore
import "http://www.testeditor.org/tsl"

TslModel:
	{TslModel}
	('package' package=QualifiedName )?
	(NL+ specification=TestSpecification)?
	NL*;

TestSpecification:
	'#' name=ID
	(NL+ description=Text)?
	(=>(NL+ '*') steps+=SpecificationStep)*;

SpecificationStep:
	contents+=InnerStepContent+
	( (NL+ contents+=StepContent) contents+=InnerStepContent+ )*;

InnerStepContent returns StepContent:
	InnerStepContentText | StepContentVariable;
	
StepContent:
	StepContentText | StepContentVariable;

InnerStepContentText returns StepContentText:
	value=(Word | '*');

StepContentText:
	value=Word;

StepContentVariable:
	value=STRING;

Word returns ecore::EString:
	ID | INT | ANY_OTHER | '.' | '#' | '\\"' | "\\'"
;

Text returns ecore::EString hidden():
	NonEmptyTextElement ( ( SPACES | NL )* NonEmptyTextElement )*;
	
NonEmptyTextElement returns ecore::EString hidden():
	ID | INT | STRING | ANY_OTHER | '.'
;

QualifiedName:
	ID (=> '.' ID)*;
	
terminal NL:
	'\r'? '\n';

terminal ID:
	'^'? ('a'..'z' | 'A'..'Z' | '_' |  'À'..'ɿ' | 'Α'..'ѯ' | 'µ') ('a'..'z' | 'A'..'Z' | '_' | '0'..'9' | 'À'..'ɿ' | 'Α'..'ѯ' | 'µ')*;

terminal INT:
	('0'..'9')+;

terminal STRING:
	'"' ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | '"'))* '"' |
	"'" ('\\' . /* 'b'|'t'|'n'|'f'|'r'|'u'|'"'|"'"|'\\' */ | !('\\' | "'"))* "'";

terminal SPACES:
	(' ' | '\t');

terminal ANY_OTHER:
	.;
